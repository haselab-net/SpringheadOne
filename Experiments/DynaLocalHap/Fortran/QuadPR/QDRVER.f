      SUBROUTINE QDRVER (KT) 
C
C     CHECK INPUT DATA FOR QUADPR
C
      DIMENSION KT(1)
C
      INTEGER  ROW,COL,INDX,P,P1,PSQ 
      COMMON /QPRCBI/ INPUTS(14),IOUTS(2),ROW,COL,INDX,MORE,P,P1,PSQ,
     1   NCALL,LENREQ,IO1,IO2,IO3
      EQUIVALENCE (INPUTS(1),ML), (INPUTS(2),NL), (INPUTS(3),MO),
     1   (INPUTS(4),NO), (INPUTS(5),MINMAX), (INPUTS(6),LENWS),
     2   (INPUTS(7),MAXIT), (INPUTS(8),KOBJ), (INPUTS(9),JIT),        
     3   (INPUTS(10),JDATA), (INPUTS(11),JPIVOT), (INPUTS(12),JSOL),
     4   (INPUTS(13),JOUT), (INPUTS(14),JWIDTH),
     5   (IOUTS(1),IERR), (IOUTS(2),ITCNT)
C
      COMMON /QPRCBR/ TOLS(2)
      EQUIVALENCE (TOLS(1),TZERO), (TOLS(2),TPIV)
C
      INTEGER ZERONE(4),ZERO12(3) 
      LOGICAL ERROR(10) 
      DATA ZERONE  / 5,8,9,11 /
      DATA ZERO12  / 10,12,13 /
C
C
      ITITLE = 0  
C
C     DEFAULT VALUES
      IF (MAXIT .LE. 0) MAXIT = 1000
      IF (TZERO .LE. 0.0) TZERO = 1.0E-7
      IF (TPIV .LE. 0.0) TPIV = 1.0E-6
      IF (JWIDTH .LT. 72) JWIDTH = 72
      IF (JWIDTH .GT. 132) JWIDTH = 132   
C
      ITITLE = 0
C
C     CHECK KT
      DO 10 I = 1, MO  
         IF (IABS(KT(I)) .GT. 1) GO TO 20
   10 CONTINUE  
      GO TO 30  
   20 CONTINUE
      WRITE (IO1,22) NCALL
   22 FORMAT ( /// 5X,'QUADPR  CALL',I4)
      WRITE (IO1,24) (KT(I),I=1,MO) 
   24 FORMAT ( / 1X,'*** ERROR, ILLEGAL VALUES IN KT.  KT CONTAINS' 
     1  / (6I12) )
      IERR = 5
      ITITLE = 1
C
C     CHECK INPUT
   30 CONTINUE
      ERROR(1) = (MO .LT. 0 .OR. MO .GT. ML)
      ERROR(2) = (NO .LT. 1 .OR. NO .GT. NL)
      ERROR(3) = (LENWS .LT. LENREQ) 
      DO 33 I = 1, 4
         J = ZERONE(I)
         ERROR(I+3) = (INPUTS(J) .NE. 0 .AND. INPUTS(J) .NE. 1)
   33 CONTINUE
      DO 34 I = 1, 3 
         J = ZERO12(I)
         ERROR(I+7) = (INPUTS(J) .LT. 0 .OR. INPUTS(J) .GT. 2)
   34 CONTINUE
      DO 35 I = 1, 10 
         IF (ERROR(I)) GO TO 38
   35 CONTINUE  
      GO TO 50
C  
   38 CONTINUE
      IERR = 5
      IF (ITITLE .EQ. 0) WRITE (IO1,22) NCALL
      WRITE (IO1,40) (INPUTS(I),I=1,14)
   40 FORMAT ( / 1X,'*** ERROR, INCONSISTENT DATA IN INPUT.' 
     1  '  INPUT CONTAINS' / 5I12 / 5I12 / 4I12 /
     2  / 5X,'THE FOLLOWING ERRORS OCCURRED.' / )
      IF (ERROR(1)) WRITE (IO1,41)
   41 FORMAT (5X,'INPUT(3) NEGATIVE OR EXCEEDS INPUT(1)')
      IF (ERROR(2)) WRITE (IO1,42)
   42 FORMAT (5X,'INPUT(4) NON-POISTIVE OR EXCEEDS INPUT(2)')
      IF (ERROR(3)) WRITE (IO1,43) LENREQ
   43 FORMAT (5X,'INPUT(6) TOO SMALL.  FOR THIS PROBLEM,',
     1   ' MUST BE AT LEAST',I8)
      DO 45 I = 1, 4
         J = ZERONE(I)
         IF (ERROR(I+3)) WRITE (IO1,46) J
   45 CONTINUE  
   46 FORMAT (5X,'INPUT(',I2,') NOT 0 OR 1')
      DO 47 I = 1, 3
         J = ZERO12(I)
         IF (ERROR(I+7)) WRITE (IO1,48) J
   47 CONTINUE
   48 FORMAT (5X,'INPUT(',I2,') NOT 0, 1, OR 2')
C
   50 CONTINUE
      RETURN
      END
