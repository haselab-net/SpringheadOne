      SUBROUTINE QUADPR (A,KT,RHS,COST,QUAD,INPUT,TOL,TITLE,PFILE,          
     *   OBJ,X,RC,DUAL,SLK,IOUT,WS)  
C
C           MADISON ACADEMIC COMPUTING CENTER
C           SUBROUTINE QUADPR FOR QUADRATIC PROGRAMMING PROGRAMS 
C           FORTRAN 77 VERSION 92.05
C
C           QUADPR MINIMIZES OR MAXIMIZES  COST*X + X*QUAD*X 
C                  SUBJECT TO     A*X + KT*'SLACK' = RHS 
C                                               X .GE. 0.
C                  (KT(I)=-1,0,+1 IF I-TH CONSTRAINT IS .GE.,.EQ.,.LE.)  
C                  THE OBJECTIVE FUNCTION SHOULD BE CONVEX (CONCAVE)
C                  FOR MINIMIZATION (MAXIMIZATION) PROBLEMS.
C                  THIS MEANS THE SYMMETRIC MATRIX QUAD+QUAD(TRANSPOSE)       
C                  SHOULD BE NONNEGATIVE (NONPOSITIVE) DEFINITE
C                  FOR MINIMIZATION (MAXIMIZATION) PROBLEMS.
C
C     CALLING SEQUENCE ...  
C     CALL QUADPR (A,KT,RHS,COST,QUAD,INPUT,TOL,TITLE,PFILE,
C    *   OBJ,X,RC,DUAL,SLK,IOUT,WS)  
C
C     IN WHAT FOLLOWS, MO = NO. OF CONSTRAINTS, NO = NO. OF VARIABLES,  
C     DIMENSION SIZES GIVEN ARE THE MINIMUM REQUIRED.
C
C A     --- MO X NO - CONSTRAINT MATRIX 
C KT    --- MO-VECTOR OF CONSTRAINT TYPES,  -1,0,+1 MEAN .GE.,.EQ.,.LE. 
C           RESPECTIVELY
C RHS   --- MO-VECTOR OF RIGHT-HAND-SIDE VALUES 
C COST  --- NO-VECTOR OF LINEAR COSTS  
C QUAD  --- NO X NO MATRIX OF QUADRATIC COSTS
C INPUT --- FIXED CONSTANTS VECTOR OF LENGTH 21 CONTAINING ...  
C ML      1 ROW DIMENSION OF A IN CALLING PROGRAM.  MINIMUM IS MO.
C NL      2 ROW DIMENSION OF QUAD IN CALLING PROGRAM.  MINIMUM IS NO.
C MO      3 NUMBER OF CONSTRAINTS.
C NO      4 NUMBER OF VARIABLES .
C MINMAX  5 =0 IF OBJECTIVE IS TO BE MINIMIZED, 
C           =1 IF OBJECTIVE IS TO BE MAXIMIZED. 
C LENWS   6 DIMENSION OF WS IN CALLING PROGRAM.  MINIMUM IS
C           2*(MO+NO)**2 + 12*(MO+NO) + 16 IF ANY EQUALITY CONSTRAINTS
C           ARE PRESENT OR  2*(MO+NO)**2 +  8*(MO+NO) + 6  
C           IF ALL CONSTRAINTS ARE INEQUALITIES.
C MAXIT   7 PIVOT LIMIT.
C KOBJ    8 COMPUTE OBJECTIVE AT SOLUTION OPTION.  =0 NO, =1 YES.
C JIT     9 OUTPUT PROBLEM PARAMETERS OPTION.   =0 NO, =1 YES.
C JDATA  10 OUTPUT PROBLEM DATA OPTION.  =0 NO,
C           =1 OUTPUT WITH A AND QUAD MATRICES IN DENSE FORM,
C           =2 OUTPUT WITH A AND QUAD MATRIX IN SPARSE FORM. 
C JPIVOT 11 OUTPUT PIVOT INFORMATION.  =0 NO, =1 YES.
C JSOL   12 OUTPUT SOLUTION REPORT OPTION.  =0 NO,
C           =1 OUTPUT BRIEF REPORT,  =2 OUTPUT FULL REPORT.
C JOUT   13 OPTION TO PRINT OR FILE OUTPUT.
C           =0 PRINT ALL OUTPUT,  =1 PRINT PROBLEM PARAMETERS AND FINAL
C           OUTPUT AND FILE OTHER OUTPUT, =2 FILE ALL OUTPUT. 
C JWIDTH 14 MAXIMUM WIDTH OF OUTPUT LINES.  A MINIMUM OF 72 AND A
C           MAXIMUM OF 132 WILL BE USED.
C
C TOL   --- TOLERANCE VECTOR OF SIZE 2 CONTAINING...
C TZERO   1 ROUND-OFF OR ZERO TOLERANCE.  IF .LE. 0 RESET TO 1.E-7 
C TPIV    2 PIVOT TOLERANCE.  IF .LE. 0 RESET TO 1.E-6
C
C TITLE --- TITLE PRINTED IN OUTPUT.  TYPE CHARACTER.
C           MAY CONTAIN UP TO 64 CHARACTERS.
C           TRUNCATED TO 64 CHARACTERS IF LONGER.
C PFILE --- FILE NAME QUADPR WRITES OUTPUT ONTO IF REQUESTED.
C           TYPE CHARACTER.  MAY CONTAIN UP TO 64 CHARACTERS.
C           IF FILE CANNOT BE OPENED, A FILE WILL BE CREATED
C           AND A MESSAGE PRINTED.  IF THE NAME IS ALL BLANKS,
C           OUTPUT TO BE FILED WILL BE DISCARDED. 
C OBJ   --- OBJECTIVE VALUE. 
C X     --- NO-VECTOR THAT WILL CONTAIN THE SOLUTION.
C RC    --- NO-VECTOR THAT WILL CONTAIN THE REDUCED COSTS.
C DUAL  --- MO-VECTOR THAT WILL CONTAIN THE DUAL SOLUTION.
C SLK   --- MO-VECTOR THAT WILL CONTAIN THE SLACK VALUES.
C IOUT  --- VECTOR OF SIZE 2, THAT WILL CONTAIN... 
C IERR    1 STATUS CODE FROM 1 TO 6.
C           1 - SOLUTION IS OPTIMAL.  
C           2 - NO SOLUTION.
C           3 - PIVOT LIMIT REACHED.  
C           4 - INVALID QUAD MATRIX.
C           5 - PROBLEM DATA ERROR.
C           6 - ALGORITHM ERROR.
C ITCNT   2 NUMBER OF PIVOTS.
C
C WS    --- WORK SPACE ARRAY OF SIZE AT LEAST
C           2*(MO+NO)**2 + 12*(MO+NO) + 16 IF ANY EQUALITY CONSTRAINTS
C           ARE PRESENT OR  2*(MO+NO)**2 +  8*(MO+NO) + 6 
C           IF ALL CONSTRAINTS ARE INEQUALITIES.
C
      DIMENSION  A(1),KT(1),RHS(1),COST(1),QUAD(1),INPUT(1),
     1   TOL(1),X(1),RC(1),DUAL(1),SLK(1),IOUT(1),WS(1)
      CHARACTER*(*)  TITLE,PFILE
C
      INTEGER  ROW,COL,INDX,MORE,P,P1,PSQ 
      COMMON /QPRCBI/ INPUTS(14),IOUTS(2),ROW,COL,INDX,MORE,P,P1,PSQ,
     1   NCALL,LENREQ,IO1,IO2,IO3
      EQUIVALENCE (INPUTS(1),ML), (INPUTS(2),NL), (INPUTS(3),MO),
     1   (INPUTS(4),NO), (INPUTS(5),MINMAX), (INPUTS(6),LENWS),
     2   (INPUTS(7),MAXIT), (INPUTS(8),KOBJ), (INPUTS(9),JIT),        
     3   (INPUTS(10),JDATA), (INPUTS(11),JPIVOT), (INPUTS(12),JSOL),
     4   (INPUTS(13),JOUT), (INPUTS(14),JWIDTH),
     5   (IOUTS(1),IERR), (IOUTS(2),ITCNT)
C
      COMMON /QPRCBR/ TOLS(2)
      EQUIVALENCE (TOLS(1),TZERO), (TOLS(2),TPIV)
C
      DOUBLE PRECISION  PIVOT
      COMMON /QPRCBD/ PIVOT
C
      CHARACTER*64  TITLES, PFILES  
      COMMON /QPRCBC/ TITLES, PFILES
C
C     QUADPR CALLS COUNTER
      SAVE NCALL0
      DATA NCALL0  / 0 /
C
C     OUTPUT UNITS FOR LINE PRINTER (TERMINAL) AND OUTPUT FILE
      DATA  LUPRNT, LUFILE  / 6, 92 /
C
C 
C     INITIALIZATIONS
C
      IO1 = LUPRNT
      IERR = 0
      ITCNT = 0
      TZERO = TOL(1)
      TPIV = TOL(2)
      NCALL0 = NCALL0 + 1 
      NCALL = NCALL0
      DO 10 I = 1, 14
         INPUTS(I) = INPUT(I)
   10 CONTINUE
      TITLES = 
     *'                                                                ' 
      L1 = LEN(TITLE)
      TITLES(1:L1) = TITLE(1:L1)
C
C     INTERNAL TABLEAU IS (MO+NO)*(MO+NO+1) IF ALL INEQUALITIES AND 
C     (MO+NO+1)*(MO+NO+2) IF ANY EQUALITIES SINCE 1 MORE ROW IS ADDED.
      IF (MO .LE. 0) GO TO 22
      DO 20 I = 1, MO
         IF (KT(I) .EQ. 0) GO TO 24
   20 CONTINUE
   22 CONTINUE
      MORE = 0
      GO TO 26
   24 CONTINUE
      MORE = 1
   26 CONTINUE
      P = MO + NO + MORE 
      P1 = P + 1
      PSQ = P**2 
C
C     SET OUTPUT UNITS AND OPEN PRINT FILE IF REQUIRED.
      IF (JOUT .NE. 1 .AND. JOUT .NE. 2) GO TO 80
      PFILES =
     *'                                                                ' 
      L1 = LEN(PFILE)
      L2 = 0
      L3 = 0
      DO 30 I = 1, L1
         IF (PFILE(I:I) .EQ. ' ') GO TO 30
         L2 = L2 + 1
         PFILES(L2:L2) = PFILE(I:I)
   30 CONTINUE
      IF (L2 .GT. 0) GO TO 40
C     BLANK FILE NAME, DISCARD OUTPUT TO BE FILED.
      JDATA = 0
      JPIVOT = 0
      IF (JOUT .EQ. 2) GO TO 90
      JIT = 0
      JSOL = 0
      GO TO 50
C     OPEN PRINT FILE
   40 CONTINUE
      OPEN (LUFILE,FILE=PFILES,STATUS='UNKNOWN',ERR=60)
      IO3 = LUFILE 
      IF (JOUT .EQ. 1) GO TO 50
      IO2 = LUFILE
      GO TO 90
   50 CONTINUE
      IO2 = LUPRNT
      GO TO 90
C     CANNOT OPEN FILE. MAKE ONE UP.
   60 CONTINUE
      IF (L3 .EQ. 1) GO TO 76
      L3 = 1
      WRITE (IO1,70) NCALL,PFILES(1:L2) 
   70 FORMAT ( //// 5X,'QUADPR  CALL',I4
     1           // 1X,'*** UNABLE TO OPEN THE FILE ', A)
      WRITE (PFILES,72) NCALL
C     FORM FILE NAME.
   72 FORMAT ('QDPR',I4.4,'.LIS',52X)
      WRITE (IO1,74) PFILES(1:12) 
   74 FORMAT ( / 5X,'OUTPUT WILL BE FILED ON ',A12) 
      GO TO 40
C     CANNOT OPEN THIS FILE EITHER.
   76 CONTINUE
      WRITE (IO1,78) LUFILE
   78 FORMAT ( / 5X,'CANNOT OPEN THIS FILE EITHER.  CHECK YOUR FILES ',
     1   'FOR THE ABOVE NAME' / 5X,'AND MAKE SURE LOGICAL UNIT',I4,
     2   ' IS CLOSED.' / )
      IERR = 5
      GO TO 200 
   80 CONTINUE
      IO2 = LUPRNT
      IO3 = LUPRNT
   90 CONTINUE
C
C     PARTITION WORKSPACE ARRAY.
      L1 = 1 + 2*(PSQ+P)
      L2 = L1 + 2*P1
      L3 = L2 + 2*P1
      L4 = L3 + P1
      LENREQ = L4 + P1 - 1
C
C     CHECK INPUT DATA PARAMETERS FOR CONSISTENCY
      CALL QDRVER (KT)
C
      IF (IERR .EQ. 5) GO TO 200
C     QUIT IF BAD VALUES (IERR = 5)
C
C     OUTPUT PROBLEM PARAMETERS AND PROBLEM DATA IF REQUESTED.
      IF (JIT .NE. 0) CALL QRPRT1
      IF (JDATA .NE. 0) CALL QRPRT2 (A,ML,KT,RHS,COST,QUAD,NL)
C
C     OUTPUT HEADER FOR INTERMEDIATE OUTPUT, IF ANY EXPECTED
      IF (JPIVOT .NE. 0) CALL QRPRT3
C
C     CONSTRUCT INITIAL TABLEAU FOR QUADRATIC PROGRAMMING.
      CALL QDPREP (A,ML,KT,RHS,COST,QUAD,NL,WS,P)
C
C     PERFORM PRINCIPAL PIVOTING TO FINAL TABLEAU. 
      CALL QDCOMP (WS,WS(L1),WS(L2),WS(L3),WS(L4))
C
C     GET SOLUTION AND OBJECTIVE VALUE IF REQUESTED.  
      CALL QDSOLN (KT,COST,QUAD,NL,WS,P,WS(L4),X,RC,DUAL,SLK,
     1   WS(L1),OBJ)
C
C     OUTPUT SOLUTION REPORT IF REQUESTED.
      IF (JSOL .NE. 0) CALL QRPRT6 (X,RC,DUAL,SLK,OBJ)
C
C     SET EXIT VALUES, IOUT.
  200 CONTINUE 
      IOUT(1) = IERR
      IOUT(2) = ITCNT
C
C     CLOSE PRINT FILE IF REQUIRED.
      IF (JOUT .GT. 0) CLOSE (LUFILE,STATUS='KEEP')
C
      RETURN
C
      END
